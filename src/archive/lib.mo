import SW "mo:stable-write-only";
import T "../migrations/types";
import ExperimentalCycles "mo:core/Cycles";
import List "mo:core/List";
import Debug "mo:core/Debug";
import Runtime "mo:core/Runtime";
import Nat "mo:core/Nat";
import Nat64 "mo:core/Nat64";
import TT "mo:timer-tool";
import Iter "mo:core/Iter";
import Prim "mo:â›”";
import Legacy = "../legacy";
import OVSFixed "mo:ovs-fixed";
import ClassPlusLib "mo:class-plus";
import Principal "mo:core/Principal";
import Inspect "../Inspect";

shared ({ caller = ledger_canister_id }) persistent actor class Archive (_args : T.Current.ArchiveInitArgs) = this {

    private func range(start: Nat, end : Nat) : Iter.Iter<Nat> {
      var i = start;
      {
        next = func() : ?Nat {
          if (i > end) return null;
          let val = i;
          i += 1;
          ?val
        }
      }
    };

    //==========================================================================
    // Message Inspection - Cycle Drain Protection
    //==========================================================================

    /// Inspect ingress messages before they are processed.
    /// Rejects calls with oversized unbounded arguments to prevent cycle drain attacks.
    system func inspect(
      {
        caller : Principal = _caller;
        arg : Blob;  // Raw message blob - check size FIRST
        msg : {
          #get_transactions : () -> { start : Nat; length : Nat };
          #get_blocks : () -> { start : Nat; length : Nat };
          #icrc3_get_blocks : () -> [T.Current.TransactionRange];
          #total_transactions : () -> ();
          #get_transaction : () -> T.Current.TxIndex;
          #remaining_capacity : () -> ();
          #get_stats : () -> ();
          #deposit_cycles : () -> ();
          #cycles : () -> ();
          #get_icrc85_stats : () -> ();
          #append_transactions : () -> [T.Current.Transaction];
        };
      }
    ) : Bool {
      // FIRST: Check raw arg size - cheapest check
      // For block queries: 100 ranges * ~20 bytes each = ~2KB max
      if (arg.size() > 10_000) {
        return false;
      };
      
      switch (msg) {
        case (#get_transactions(getArgs)) {
          Inspect.inspectLegacyBlocks(getArgs(), null);
        };
        case (#get_blocks(getArgs)) {
          Inspect.inspectLegacyBlocks(getArgs(), null);
        };
        case (#icrc3_get_blocks(getArgs)) {
          Inspect.inspectGetBlocks(getArgs(), null);
        };
        // No validation needed for these - bounded types
        case (#total_transactions(_)) true;
        case (#get_transaction(_)) true;
        case (#remaining_capacity(_)) true;
        case (#get_stats(_)) true;
        case (#deposit_cycles(_)) true;
        case (#cycles(_)) true;
        case (#get_icrc85_stats(_)) true;
        // Inter-canister call from ledger - caller check in function
        case (#append_transactions(_)) true;
      };
    };

    /// ICRC-85 namespace for Archive Open Value Sharing
    let ICRC85_NAMESPACE = "org.icdevs.icrc85.icrc3archive";
    let ICRC85_TIMER_NAMESPACE = "icrc85:ovs:shareaction:icrc3archive";
    let ONE_DAY = 86_400_000_000_000; // 1 day in nanoseconds
    let ONE_XDR_OF_CYCLES = 1_000_000_000_000;  // 1 XDR worth of cycles (~1T)


    transient let canisterId = Principal.fromActor(this);

    transient let debug_channel = {
      announce = false;
      append = false;
      get = false;
      icrc85 = false;
    };

    debug if(debug_channel.announce) Debug.print("new archive created with the following args" # debug_show(_args));

    type Transaction = T.Current.Transaction;
    type MemoryBlock = {
        offset : Nat64;
        size : Nat;
    };

    public type InitArgs = T.Current.ArchiveInitArgs;

    public type AddTransactionsResponse = T.Current.AddTransactionsResponse;
    public type TransactionRange = T.Current.TransactionRange;
    public type ArchiveStats = T.Current.ArchiveStats;

    var initial_args = _args;
    transient var args = _args;

    var memstore = SW.init({
      //maxRecords = args.maxRecords;
      indexType = initial_args.indexType;
      maxPages = 62500;
    });

    transient let sw = SW.StableWriteOnly(?memstore);


    ///////////
    // ICRC-85 Open Value Sharing
    // Should compute to one extra XDR per 1,000,000 records stored
    ///////////

    // Configuration
    let ovsConfig : OVSFixed.InitArgs = {
        namespace = ICRC85_NAMESPACE;
        publicNamespace = ICRC85_TIMER_NAMESPACE;
        baseCycles = ONE_XDR_OF_CYCLES;
        actionDivisor = 1;
        actionMultiplier = 1_000_000_000;
        maxCycles = ONE_XDR_OF_CYCLES * 100;
        initialWait = ?(ONE_DAY * 7); 
        period = null; // default 30 days
        asset = null; //default Cycles
        platform = null;  //default ICP
        resetAtEndOfPeriod = false; //storage....per record
    };

    //icrc1 needs its own classmanager
    transient var org_icdevs_class_plus_manager = ClassPlusLib.ClassPlusInitializationManager<system>(ledger_canister_id, canisterId, true);

    var org_icdevs_timer_tool_state = TT.initialState();
    
    // Build timerTool environment with collector if provided
    func getTimerToolEnv() : TT.Environment {
      {
        advanced = ?{
          icrc85 = ?{
            kill_switch = null;
            handler = null;
            period = null;  // default 30 days
            initialWait = ?(ONE_DAY * 7);  // 7 day grace period
            asset = null;   // default cycles
            platform = null; // default ICP
            tree = null;
            collector = args.icrc85Collector;  // Use the collector passed to archive
          };
        };
        syncUnsafe = null;
        reportExecution = null;
        reportError = null;
        reportBatch = null;
      }
    };
          
    transient var org_icdevs_timer_tool = TT.Init({
      org_icdevs_class_plus_manager = org_icdevs_class_plus_manager;
      initialState = org_icdevs_timer_tool_state;
      args = null;
      pullEnvironment = ?getTimerToolEnv;
      onInitialize = null;
      onStorageChange = func(state: TT.State) {
        org_icdevs_timer_tool_state := state;
      }
    });


    func getOVSEnv() : OVSFixed.Environment {
      {
          var org_icdevs_timer_tool = ?org_icdevs_timer_tool();
          var collector = args.icrc85Collector;
          advanced = null;
      }
    };

          
    var org_icdevs_ovs_fixed_state = OVSFixed.initialState();

    transient var _ovs = OVSFixed.Init({
        org_icdevs_class_plus_manager = org_icdevs_class_plus_manager;
        args = ?ovsConfig;
        pullEnvironment = ?getOVSEnv;
        onInitialize = null;
        initialState = org_icdevs_ovs_fixed_state;
        onStorageChange = func(_state : OVSFixed.State){
          org_icdevs_ovs_fixed_state := _state;
        };
    });
/*
    // Track if ICRC-85 timer has been initialized
    stable var icrc85TimerScheduled = false;

    // =====================================
    // ICRC-85 Functions (defined first for use in append_transactions)
    // =====================================

    /// Calculate cycles to share based on ICRC-85 formula for archives
    /// Formula: Base 1 XDR + 1 XDR per 1,000,000 records stored, capped at 100 XDR
    func calculate_cycles_to_share() : (cycles: Nat, actions: Nat) {
      let actions = if(icrc85State.activeActions > 0){
        icrc85State.activeActions;
      } else { 1 };

      // Base: 1 XDR per month (~1T cycles)
      var cyclesToShare = ONE_XDR_OF_CYCLES;

      // +1 XDR per 1,000,000 records (archives store a lot more data)
      if(actions > 0){
        let additional = Nat.div(actions, 1_000_000);
        cyclesToShare := cyclesToShare + (additional * ONE_XDR_OF_CYCLES);
        
        // Cap at 100 XDR
        if(cyclesToShare > 100 * ONE_XDR_OF_CYCLES) {
          cyclesToShare := 100 * ONE_XDR_OF_CYCLES;
        };
      };

      (cyclesToShare, actions);
    };

    /// Share cycles with the OVS collector
    func share_cycles() : async* () {
      debug if(debug_channel.icrc85) Debug.print("Archive: in share_cycles");

      let (cyclesToShare, actions) = calculate_cycles_to_share();
      
      debug if(debug_channel.icrc85) Debug.print("Archive: actions=" # debug_show(actions) # ", cycles=" # debug_show(cyclesToShare));

      // Reset actions counter before sharing
      icrc85State.activeActions := 0;

      // Build ICRC-85 environment from init args
      // Archive doesn't use ClassPlus, so we create a minimal environment
      let icrc85Env : OVSFixed.Environment = {
        var org_icdevs_class_plus_manager = null;
        var org_icdevs_timer_tool = null;
        var collector = _args.icrc85Collector;
        advanced = ?{
          kill_switch = ?false;
          handler = null;
          tree = null;
        };
      };

      try {
        await* OVSFixed.shareCycles<system>({
          environment = icrc85Env;
          namespace = ICRC85_NAMESPACE;
          actions = actions;
          schedule = func<system>(_period: Nat) : async* () {
            // Schedule next share
            ignore Timer.setTimer<system>(#nanoseconds(SHARING_PERIOD), func() : async () {
              await* share_cycles();
            });
          };
          cycles = cyclesToShare;
          period = ?SHARING_PERIOD;
          asset = ?"cycles";
          platform = ?"icp";
        });
        icrc85State.lastActionReported := ?Int.abs(Time.now());
      } catch(e){
        // Restore actions on error - will retry next period
        icrc85State.activeActions := actions;
        debug if(debug_channel.icrc85) Debug.print("Archive: error sharing cycles: " # Error.message(e));
        // Schedule retry
        ignore Timer.setTimer<system>(#nanoseconds(SHARING_PERIOD), func() : async () {
          await* share_cycles();
        });
      };
    };

    // Auto-initialize timer on first append_transactions if not already scheduled
    private func ensureTimerScheduled<system>() {
      if (not icrc85TimerScheduled) {
        icrc85TimerScheduled := true;
        ignore Timer.setTimer<system>(#nanoseconds(GRACE_PERIOD), func() : async () {
          await* share_cycles();
        });
      };
    };
    */

    // =====================================
    // Main Archive Functions
    // =====================================

    public shared ({ caller }) func append_transactions(txs : [Transaction]) : async AddTransactionsResponse {

      debug if(debug_channel.append) Debug.print("adding transactions to archive" # debug_show(txs));

      if (caller != ledger_canister_id) {
          return #err("Unauthorized Access: Only the ledger canister can access this archive canister");
      };

      // Ensure ICRC-85 timer is scheduled on first append
      //ensureTimerScheduled<system>();

      var recordsAdded = 0;
      label addrecs for(thisItem in txs.vals()){
        let stats = sw.stats();
        if(stats.itemCount >= args.maxRecords){
          debug if(debug_channel.append) Debug.print("braking add recs");
          break addrecs;
        };
        ignore sw.write(to_candid(thisItem));
        recordsAdded += 1;
      };

      // ICRC-85: Track records added for cycle sharing
      org_icdevs_ovs_fixed_state.activeActions +=  recordsAdded;

      let final_stats = sw.stats();
      if(final_stats.itemCount >= args.maxRecords){
        return #Full(final_stats);
      };
      #ok(final_stats);
    };

    func total_txs() : Nat {
        sw.stats().itemCount;
    };

    public shared query func total_transactions() : async Nat {
        total_txs();
    };

    public shared query func get_transaction(tx_index : T.Current.TxIndex) : async ?Transaction {
        return _get_transaction(tx_index);
    };

    public shared query func get_transactions(args: {start : Nat; length: Nat}) : async {
      transactions : [Legacy.Transaction];
    } {
        // Guard for inter-canister calls
        Inspect.guardLegacyBlocks(args, null);
        
        if(args.length > 100000) {
            Runtime.trap("You cannot request more than 100000 transactions at once");
        };
        let results = List.empty<Legacy.Transaction>();
        if(args.length == 0) return { transactions = [] };
        for(thisItem in range(args.start, args.start + args.length - 1)){
            switch(_get_transaction(thisItem)){
                case(null){
                    //should be unreachable...do we return an error?
                };
                case(?val){
                    let items = Legacy.convertICRC3ToLegacyTransaction([val]);
                    if(items.size() == 0){
                        List.add(results,{
                          burn = null;
                          kind = "not_found";
                          mint = null;
                          approve = null;
                          timestamp = 0;
                          transfer = null;
                        } : Legacy.Transaction)
                    } else {
                        for (item in items.vals()) {
                            List.add(results, item);
                        };
                    };
                };
            };
        };
        return {
          transactions = List.toArray(results);
        };
    };

    // Legacy get_blocks for Rosetta compatibility
    // Rosetta's icrc-ledger-agent calls this method on archive canisters
    // Returns blocks in the format expected by BlockRange: { blocks: [Value] }
    public shared query func get_blocks(args: {start : Nat; length: Nat}) : async {
      blocks : [Transaction];
    } {
        // Guard for inter-canister calls
        Inspect.guardLegacyBlocks(args, null);
        
        if(args.length > 100000) {
            Runtime.trap("You cannot request more than 100000 blocks at once");
        };
        let results = List.empty<Transaction>();
        if(args.length == 0) return { blocks = [] };
        for(thisItem in range(args.start, args.start + args.length - 1)){
            switch(_get_transaction(thisItem)){
                case(null){
                    // Block not found - skip
                };
                case(?val){
                    List.add(results, val);
                };
            };
        };
        return {
          blocks = List.toArray(results);
        };
    };

    private func _get_transaction(tx_index : T.Current.TxIndex) : ?Transaction {
        let stats = sw.stats();
        debug if(debug_channel.get) Debug.print("getting transaction" # debug_show(tx_index, args.firstIndex, stats));
       
        let target_index =  if(tx_index >= args.firstIndex) Nat.sub(tx_index, args.firstIndex) else Runtime.trap("Not on this canister requested " # Nat.toText(tx_index) # "first index: " # Nat.toText(args.firstIndex));
        debug if(debug_channel.get) Debug.print("target" # debug_show(target_index));
        if(target_index >= stats.itemCount) Runtime.trap("requested an item outside of this archive canister. first index: " # Nat.toText(args.firstIndex) # " last item" # Nat.toText(args.firstIndex + stats.itemCount - 1));
        debug if(debug_channel.get) Debug.print("target" # debug_show(target_index));
        let ?blob = sw.read(target_index) else return null;
        let t = from_candid(blob) : ?Transaction;
        return t;
    };

    public shared query func icrc3_get_blocks(req : [T.Current.TransactionRange]) : async T.Current.GetTransactionsResult {
      // Guard for inter-canister calls
      Inspect.guardGetBlocks(req, null);

      debug if(debug_channel.get) Debug.print("request for archive blocks " # debug_show(req));

      let transactions = List.empty<{id:Nat; block: Transaction}>();
      for(thisArg in req.vals()){
        // Skip if length is 0 - no blocks to retrieve
        if(thisArg.length != 0) {
          // Calculate the end index (exclusive)
          let endIndex = thisArg.start + thisArg.length;
          if (thisArg.length > 0) {
            for(thisItem in range(thisArg.start, endIndex - 1)){
              debug if(debug_channel.get) Debug.print("getting" # debug_show(thisItem));
              switch(_get_transaction(thisItem)){
                case(null){
                  //should be unreachable...do we return an error?
                };
                case(?val){
                  debug if(debug_channel.get) Debug.print("found" # debug_show(val));
                  List.add(transactions, {id = thisItem; block = val}); // Use thisItem directly as the ID
                };
              };
            };
          };
        };
      };

      return { 
          blocks = List.toArray(transactions);
          archived_blocks = [];
          log_length =  0;
        };
    };

    public shared query func remaining_capacity() : async Nat {
        args.maxRecords - sw.stats().itemCount;
    };

    /// Get comprehensive statistics about this archive canister
    public shared query func get_stats() : async ArchiveStats {
        let stats = sw.stats();
        let totalRecords = stats.itemCount;
        // Calculate stable memory from region stats instead of deprecated Prim.stableMemorySize()
        let indexPages = switch(stats.memory.pages) {
          case(?p) Nat64.toNat(p);
          case(null) 0;
        };
        let dataPages = Nat64.toNat(stats.currentPages);
        {
          total_records = totalRecords;
          first_block_index = args.firstIndex;
          last_block_index = if (totalRecords > 0) { args.firstIndex + totalRecords - 1 } else { args.firstIndex };
          max_records = args.maxRecords;
          remaining_capacity = args.maxRecords - totalRecords;
          cycles_balance = ExperimentalCycles.balance();
          heap_memory = Prim.rts_heap_size();
          stable_memory_pages = dataPages + indexPages;
          ledger_canister = ledger_canister_id;
        };
    };

    /// Deposit cycles into this archive canister.
    public shared func deposit_cycles() : async () {
        let amount = ExperimentalCycles.available();
        let accepted = ExperimentalCycles.accept<system>(amount);
        assert (accepted == amount);
    };

    /// Get the remaining cylces on the server
    public query func cycles() : async Nat {
        ExperimentalCycles.balance();
    };

    ///////////
    // ICRC-85 Public Endpoints
    ///////////

    /// Get ICRC-85 statistics for the archive
    public query func get_icrc85_stats() : async {
      activeActions: Nat;
      lastActionReported: ?Nat;
      nextCycleActionId: ?Nat;
    } {
      {
        activeActions = org_icdevs_ovs_fixed_state.activeActions;
        lastActionReported = org_icdevs_ovs_fixed_state.lastActionReported;
        nextCycleActionId = org_icdevs_ovs_fixed_state.nextCycleActionId;
      };
    };

};